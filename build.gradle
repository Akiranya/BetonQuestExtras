plugins {
    id 'java'
    id "io.freefair.lombok" version "6.5.1"
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

ext.pluginNameUpper = "BetonQuest-Mewcraft"
ext.majorVersion = 1
ext.minorVersion = 1
ext.minecraftVersion = "1.19"

group = 'cc.mewcraft'
archivesBaseName = "${pluginNameUpper}-mc$minecraftVersion"
version = "$majorVersion.$minorVersion" as Object

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        url "https://repo.purpurmc.org/snapshots"
        content {
            includeGroup("org.purpurmc.purpur")
        }
    }

    maven {
        url "https://jitpack.io"
        content {
            includeGroup("com.github.TownyAdvanced")
            includeGroup("com.github.LoneDev6")
        }
        metadataSources {
            artifact()
        }
    }

    maven {
        url "https://betonquest.org/nexus/repository/betonquest/"
        content {
            includeGroup("org.betonquest")
        }
        metadataSources {
            artifact()
        }
    }
}

dependencies {
    // API
    compileOnly("org.purpurmc.purpur:purpur-api:1.19.2-R0.1-SNAPSHOT")

    // Plugin library
    compileOnly('me.lucko:helper:5.6.10')

    // External plugins
    compileOnly('org.betonquest:betonquest:2.0.0-SNAPSHOT')
    compileOnly('com.github.TownyAdvanced:Towny:0.98.3.9')
    compileOnly('com.github.LoneDev6:api-itemsadder:3.0.0')

    // Test
    testCompileOnly('org.purpurmc.purpur:purpur-api:1.19.2-R0.1-SNAPSHOT')
    testCompileOnly('org.junit.jupiter:junit-jupiter:5.9.0')
    testCompileOnly('me.lucko:helper:5.6.10')
}

tasks {
    compileJava {
        options.encoding = 'UTF-8'
    }
    javadoc {
        options.encoding = 'UTF-8'
    }
    processResources {
        filteringCharset = 'UTF-8'
    }
}

processResources {
    filesMatching("**/plugin.yml") {
        expand 'version': project.version
    }
}

task deploy {
    dependsOn(jar)
    doLast {
        exec {
            def jarFileName = jar.archiveFile.get().toString()
            print jarFileName
            workingDir '.'
            commandLine 'scp', jarFileName, 'dev:data/devmisc/plugins'
        }
    }
}