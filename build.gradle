plugins {
    id 'java'
    id "io.freefair.lombok" version "6.5.1"
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

group = 'cc.mewcraft'
version = "1.3.1"
archivesBaseName = "BetonQuestExtras"

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://repo.purpurmc.org/snapshots"
        content {
            includeGroup("org.purpurmc.purpur")
        }
    }
    maven {
        url "https://jitpack.io"
        content {
            includeGroup("com.github.LoneDev6")
            includeGroup("com.github.TownyAdvanced")
            includeGroup("com.github.DieReicheErethons")
        }
    }
    maven {
        url "https://betonquest.org/nexus/repository/betonquest/"
        content {
            includeGroup("org.betonquest")
        }
    }
}

dependencies {
    // API
    compileOnly("org.purpurmc.purpur:purpur-api:1.19.3-R0.1-SNAPSHOT")

    // Plugin library
    compileOnly('me.lucko:helper:5.6.13')

    // External plugins
    compileOnly('org.betonquest:betonquest:2.0.0-SNAPSHOT')
    compileOnly('com.github.TownyAdvanced:Towny:0.98.6.0')
    compileOnly('com.github.LoneDev6:api-itemsadder:3.0.0')
    compileOnly('com.github.DieReicheErethons:Brewery:3.1')

    // Test
    testCompileOnly('org.purpurmc.purpur:purpur-api:1.19.3-R0.1-SNAPSHOT')
    testCompileOnly('org.junit.jupiter:junit-jupiter:5.9.0')
    testCompileOnly('me.lucko:helper:5.6.13')
}

tasks {
    compileJava {
        options.encoding = 'UTF-8'
    }
    javadoc {
        options.encoding = 'UTF-8'
    }
    processResources {
        filteringCharset = 'UTF-8'
    }
}

processResources {
    filesMatching("**/plugin.yml") {
        expand 'version': project.version
    }
}

tasks.register('deploy') {
    dependsOn(jar)
    doLast {
        exec {
            def jarFileName = jar.archiveFile.get().toString()
            workingDir '.'
            commandLine 'scp', jarFileName, 'dev:data/dev/jar'
        }
    }
}